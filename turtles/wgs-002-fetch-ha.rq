PREFIX onto: <http://www.ontotext.com/>
PREFIX f: <https://flu-crew.org/term/>
PREFIX fid: <https://flu-crew.org/id/>
PREFIX world: <https://flu-crew.org/geo/country/>
PREFIX usa: <https://flu-crew.org/geo/country/usa/>

SELECT DISTINCT
  # required for upload to IRD
  ?h_genbank
  ?strain
  ?subtype
  ?date
  (GROUP_CONCAT(DISTINCT ?source; separator="+") as ?sources)
  # for my own use
  (GROUP_CONCAT(DISTINCT ?const; separator="+") as ?consts)
  (GROUP_CONCAT(DISTINCT ?ha_clade; separator="+") as ?ha_clades)
  (GROUP_CONCAT(DISTINCT ?na_clade; separator="+") as ?na_clades)
  ?segment
  ?seq
FROM onto:disable-sameAs
WHERE {
  ?sid f:barcode ?barcode .
  ?sid f:strain_name ?strain .
  ?sid f:host ?host .
  ?sid f:country/f:code ?country .
  OPTIONAL { ?sid f:state/f:name ?state . }
  ?sid f:date ?date .

  # Keep only the A0 strains (e.g., not the TOSU strains or gisaid crap)
  FILTER REGEX(?barcode, "^A0") .
  FILTER (?host = "swine") .
  FILTER (?country = "USA") .
  BIND(MONTH(?date) as ?month) .
  FILTER BOUND(?month) .
  FILTER (
    YEAR(?date) = 2019 ||
    (YEAR(?date) > 2015 &&
     (REGEX(?barcode, "A0...8[1-4]..") || (BOUND(?const) && ?const != "------"))
    )) .

  ?sid f:has_genbank ?h_genbank .
  ?sid f:has_genbank ?n_genbank .

  ?sid f:has_segment ?hagid .
  ?hagid f:segment_name "HA" .
  ?hagid f:genbank_id ?h_genbank .
  ?hagid f:has_feature/f:isolation_source ?source .

  ?sid f:has_segment ?nagid .
  ?nagid f:segment_name "NA" .
  ?nagid f:genbank_id ?n_genbank .

  ?hagid f:dnaseq ?seq .
  BIND("HA" as ?segment) .

  ?sid f:ha ?ha_clade .
  ?sid f:na ?na_clade .

  OPTIONAL { ?sid f:constellation ?const . }
  OPTIONAL { ?sid f:subtype ?subtype . }
}
GROUP BY ?h_genbank ?strain ?subtype ?date ?segment ?seq
