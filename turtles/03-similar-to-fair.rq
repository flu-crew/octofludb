PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX f: <https://flu-crew.org/term/>
PREFIX onto: <http://www.ontotext.com/>

SELECT DISTINCT ?strain1 ?strain2
FROM onto:disable-sameAs
WHERE {
    # require the query to be a fair sequence
    ?sid1 f:tag/f:name "fair" .

    # link the query segments to their strain
    ?sid1 f:has_segment ?qseqid .
    ?sid1 f:strain_name ?strain1 .

    # link to related strains
    ?sid2 f:has_segment ?sseqid .
    ?sid2 f:strain_name ?strain2 .

    # identify related sequences
    ?bid f:qseqid ?qseqid .
    ?bid f:sseqid ?sseqid .
    ?bid f:pident ?pident .
    FILTER (?pident > 95 && ?pident <= 99.9) .

    FILTER NOT EXISTS { ?sid2 f:tag/f:name "fair" } .
}
